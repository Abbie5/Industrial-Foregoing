buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.3-2"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


String build = ""
if (project.hasProperty("teamcity")) {
    build = System.getenv("BUILD_NUMBER")
}

if (build == null || build == ""){
    version = "${project.minecraft_version}-${project.mod_version}"
}else{
    version = "${project.minecraft_version}-${project.mod_version}-"+ build
}
group = "com.buuz135.industrial.IndustrialForegoing" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "industrialforegoing"

sourceCompatibility = "1.8"
targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12-14.21.1.2443"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20170624"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    replace '@MINECRAFT-VERSION@', project.minecraft_version
    replace '@VERSION@', version
}

configurations {
    all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }
}

repositories {
    mavenCentral()
    maven { url 'http://maven.epoxide.xyz' }
    maven { url 'http://maven.ndrei.net' }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
    maven { url 'http://maven.covers1624.net/' }
    maven { // CraftTweaker
        name 'jared maven'
        url "http://maven.blamejared.com/"
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.3-2"
    compile "net.darkhax.tesla:Tesla-1.12:${tesla_version}:deobf"
    compile ("net.ndrei:tesla-core-lib:${project.minecraft_version}-${project.tesla_core_version}:deobf") { changing = true }
    compileOnly "org.projectlombok:lombok:1.16.16"
    // compile against the JEI API
    deobfCompile "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
    compile "cofh:RedstoneFlux:1.12-$redstoneflux_version:universal"
    compile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.+"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': version, 'mcversion': project.minecraft_version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }
    extension = 'jar'
    classifier = 'deobf'
}

artifacts {
    archives devJar
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

uploadArchives {
    repositories {
        mavenDeployer {
            Configuration = configurations.deployerJars
            //repository(url: System.properties['URL']) {
            repository(url: "ftp://dyonovan.com/srv/http/maven2") {
                authentication(userName: "${username}", password: "${password}")
            }
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName

                pom.project {
                    name = project.archivesBaseName
                    packaging 'jar'
                    description 'Industrial Foregoing'
                    url 'https://github.com/Buuz135/Industrial-Foregoing'
                    scm {
                        url 'https://github.com/Buuz135/Industrial-Foregoing'
                        connection 'scm:git:git@github.com/Buuz135/Industrial-Foregoing'
                        developerConnection 'scm:git:git@github.com/Buuz135/Industrial-Foregoing'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/Buuz135/Industrial-Foregoing/issues'
                    }
                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'Buuz135'
                            name 'Buuz135'
                            roles {
                                role 'developer'
                            }
                        }
                    }
                }
            }
        }
    }
}